---
page_title: "OPA Integration - plancost"
description: "Learn how to integrate Open Policy Agent (OPA) with plancost to enforce advanced cost policies."
---

# OPA Integration

While `plancost` provides built-in [Cost Guardrails](guardrails.md) for budget enforcement, you may require more complex or custom policies. Integrating with [Open Policy Agent (OPA)](https://www.openpolicyagent.org/) allows you to write flexible policies using Rego to enforce cost constraints based on the estimates generated by `plancost`.

## Workflow

The general workflow involves generating a Terraform plan, converting it to JSON, and then evaluating it against your OPA policies.

### 1. Generate the plan

First, run `terraform plan` and output the plan to a file.

```bash
terraform plan -out=tfplan
```

### 2. Convert plan to JSON

Convert the binary plan file to a JSON format that OPA can consume.

```bash
terraform show -json tfplan > tfplan.json
```

### 3. Run OPA

You can evaluate the plan using the `opa` CLI or tools like `conftest`.

#### Using OPA CLI

```bash
opa eval --format pretty --input tfplan.json --data policy/cost.rego "data.main.deny"
```

#### Using Conftest

If you prefer [Conftest](https://www.conftest.dev/):

```bash
conftest test tfplan.json -p policy/cost.rego
```

## Example Policies

### 1. Total Cost Limit

Here is an example `policy/cost.rego` file that denies the plan if the monthly cost estimate exceeds $50.

```rego
package main

deny contains msg if {
	resource := input.resource_changes[_]
	resource.type == "plancost_estimate"
	resource.name == "main"
	cost := resource.change.after.monthly_cost
	to_number(cost) >= 50
	msg := sprintf("Monthly cost estimate is too high: $%v (limit: $50)", [cost])
}
```

**Output:**

```text
[
  "Monthly cost estimate is too high: $71.81 (limit: $50)"
]
```

### 2. Cost Increase Limit

This policy denies the plan if the monthly cost *increase* exceeds $10.

```rego
package main

deny contains msg if {
    resource := input.resource_changes[_]
    resource.type == "plancost_estimate"
    resource.name == "main"
    
    after_cost := to_number(resource.change.after.monthly_cost)
    before_cost := get_before_cost(resource)
    diff := after_cost - before_cost
    
    diff >= 10
    msg := sprintf("Monthly cost increase is too high: $%v (limit: $10)", [diff])
}

get_before_cost(resource) := cost if {
    resource.change.before != null
    cost := to_number(resource.change.before.monthly_cost)
}

get_before_cost(resource) := 0 if {
    resource.change.before == null
}
```

**Output:**

```text
[
  "Monthly cost increase is too high: $143.61 (limit: $10)"
]
```

## Built-in Alternatives

We recommend using the built-in [Cost Guardrails](guardrails.md) feature for total cost limits. It is natively integrated into the provider and does not require installing or configuring external tools like OPA.
